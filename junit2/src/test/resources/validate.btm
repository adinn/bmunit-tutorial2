#######################################################################
# JBoss, Home of Professional Open Source
# Copyright 2015, Red Hat and individual contributors as identified
# by the @authors tag. See the copyright.txt in the distribution for a
# full listing of individual contributors.
#
# This is free software; you can redistribute it and/or modify it
# under the terms of the GNU Lesser General Public License as
# published by the Free Software Foundation; either version 2.1 of
# the License, or (at your option) any later version.
#
# This software is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public
# License along with this software; if not, write to the Free
# Software Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA
# 02110-1301 USA, or see the FSF site: http://www.fsf.org.
#
# @authors Andrew Dinn
#

# A rule which tracks whether or not a pipe close
# in the downstream process causes an IOException for
# the upstream CharSequenceSource. If so a Byteman flag
# labelled with the string "IOException occurred" is
# set

RULE flag IOException
CLASS IOException
METHOD <init>
AT EXIT
IF callerMatches("CharSequenceSource.produce", true, 3)
DO traceStack("*** IO exception created here ***\n", 5);
   traceln("***");
   flag("IOException occurred")
ENDRULE

# A rule injected into test code which allows it to
# clear the Byteman flag labelled "IOException occurred"

RULE clear IO Exception
CLASS BytemanJUnitTests2
METHOD clearExceptionFlag()
AT ENTRY
IF TRUE
DO clear("IOException occurred")
ENDRULE

# A rule injected into test code which allows it to
# test the Byteman flag labelled "IOException occurred"

RULE check for IO Exception
CLASS BytemanJUnitTests2
METHOD checkExceptionFlag()
AT ENTRY
IF TRUE
DO RETURN flagged("IOException occurred")
ENDRULE
